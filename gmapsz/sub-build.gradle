def zipnm = "zk-"
switch ( project.name ) {
    case "gmapsz":
        zipnm += "Gmaps"
        break
    case "ckez":
    	zipnm += "CKEditor"
        break
}
println project.name
def isOfficial = project.hasProperty("target") && "official" == project.getProperty("target") ? true : ""
version = isOfficial && version.contains("-FL") ? version.substring(0, version.indexOf("-FL")) : version
//def fldate = isOfficial ? "" : new Date().format("yyyy-MM-dd")
def fldate = "" //handled by jenkins build
def versionString = isOfficial ? version : "${version}-${fldate}"
println ((isOfficial ? "official" : "FL") + " version: " + versionString)

println version

processResources {
	exclude '**/web/js' // Handled by zipJs
}

javadoc {
	options.addStringOption("Xdoclint:none", "--allow-script-in-comments")
	options.bottom("Copyright &amp;copy; 2005-${new Date().format("yyyy")} Potix Corporation. All Rights Reserved.")
}

jar {
	archiveName project.name + ".jar"
	dependsOn 'zipJs'
}

build {
	dependsOn 'sourceJar'
	dependsOn 'makeBinZip'
	dependsOn 'makeSrcZip'
	dependsOn 'makeBundle'
}

task zipJs(type: JavaExec) {
	classpath("dist/lib/ext/zkjszips.jar")
	args("src/main/resources/web/js", "${buildDir}/resources/main/web/js")
	jvmArgs("-Xss1024k")
}

task sourceJar(type: Jar) {
	outputs.upToDateWhen { false }
	doFirst {
		from sourceSets.main.java
		archiveName project.name + "-sources.jar"
	}
}

task prepareFiles {
	dependsOn 'javadoc'
	dependsOn 'jar'
	dependsOn 'sourceJar'
	doFirst {
		def name = project.name
		def binFolderName = zipnm + "-bin-" + versionString
		def srcFolderName = zipnm + "-src-" + versionString
		copy {
			from "${buildDir}/libs/" + name + "-sources.jar"
			into "${buildDir}/binzip/" + binFolderName + "/dist/src"
		}
		copy {
			from "${buildDir}/libs/" + name + ".jar"
			into "${buildDir}/binzip/" + binFolderName + "/dist/lib"
		}
		copy {
			from "../zkdoc"
			into "${buildDir}/binzip/" + binFolderName + "/doc"
		}
		copy {
			from "${buildDir}/docs"
			into "${buildDir}/binzip/" + binFolderName
		}
		copy {
			from (".") {
				exclude("build")
				exclude("dist")
				exclude(".gradle")
			}
			into "${buildDir}/srczip/" + srcFolderName + "/${name}/"
		}
		copy {
			from "../zkdoc"
			into "${buildDir}/srczip/" + srcFolderName + "/doc"
		}
		delete("${buildDir}/srczip/" + srcFolderName + "/${name}/pom.xml")
		copy{
			from("${buildDir}/srczip/" + srcFolderName + "/${name}/")
			into("${buildDir}/srczip/" + srcFolderName + "/${name}/")
			include '.flattened-pom.xml'
			rename { String fileName ->
				fileName.replace(".flattened-pom.xml", "pom.xml")
			}
		}
		delete("${buildDir}/srczip/" + srcFolderName + "/${name}/.flattened-pom.xml")
	}
}

task makeBinZip(type: Zip, dependsOn: prepareFiles) {
	appendix "bin"
	baseName zipnm
	classifier fldate
	from("${buildDir}/binzip/")
	destinationDir(file("${buildDir}/distributions/zk"))
}

task makeSrcZip(type: Zip, dependsOn: prepareFiles) {
	appendix "src"
	baseName zipnm
	classifier fldate
	from("${buildDir}/srczip/")
	destinationDir(file("${buildDir}/distributions/zk"))
}


import org.apache.tools.ant.filters.ReplaceTokens

task prepareBundleFiles(dependsOn: ['jar', 'sourceJar']) {
	doLast {
		copy {
			from "${buildDir}/libs/"
			from ("${project.projectDir}") {
				include ".flattened-pom.xml"
                filter (ReplaceTokens, tokens: [version: versionString.toString()])
			}
			into "${buildDir}/maven/"
			rename { String fileName ->
				fileName.replace("${project.name}", "${project.name}-${versionString}")
			}
			rename { String fileName ->
				fileName.replace(".flattened-pom.xml", "pom.xml")
			}
		}
	}
}

task makeBundle(type: Jar, dependsOn: prepareBundleFiles) {
	doLast {
		println "Create ${project.name}-" + versionString + "-bundle.jar"
	}
	baseName "${project.name}"
	version versionString
	classifier "bundle"
	from("${buildDir}/maven/")
	destinationDir(file("${buildDir}/distributions/maven"))
}
